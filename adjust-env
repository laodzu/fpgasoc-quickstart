echo Reset environment
setenv addcons 'setenv bootargs ${bootargs} console=${console},${baudrate}'
setenv addip 'setenv bootargs ${bootargs} ip=${ipaddr}:${serverip}:${gatewayip}:${netmask}:${hostname}:${netdev}:off'
setenv addmisc 'setenv bootargs ${bootargs} uio_pdrv_genirq.of_id=generic-uio'
setenv addmem 'setenv bootargs ${bootargs} mem=${mem}'
setenv baudrate '115200'
setenv bootcmd 'run mmc_mmc'
setenv bootdelay '10'
setenv bootdir 'boot'
setenv bootfile 'uImage'
setenv console 'ttyS0'
setenv ethaddr '00:4E:8B:C3:E5:9B'
setenv fdt_addr_r '0xf00000'
setenv fdt_file 'socfpga_socrates.dtb'
setenv fpga_image 'fpga_image.rbf'
setenv fpga_load 'mmc rescan; ext2load mmc 0:${mmcpart} 0x100000 ${bootdir}/${fpga_image} && fpga load 0 0x100000 ${filesize} && run bridge_enable_handoff'
setenv ipaddr '192.168.42.5'
setenv kernel_addr_r '0x10000000'
setenv mem '1024M'
setenv mmc_mmc 'mmc rescan; ext2load mmc 0:${mmcpart} ${kernel_addr_r} ${bootdir}/${bootfile}; ext2load mmc 0:${mmcpart} ${fdt_addr_r} ${bootdir}/${fdt_file}; run mmcargs addcons addmisc addmem; bootm ${kernel_addr_r} - ${fdt_addr_r}'
setenv mmcargs 'setenv bootargs root=${mmcroot} rw rootwait'
setenv mmcpart '2'
setenv mmcroot '/dev/mmcblk0p2'
setenv net_mmc 'tftp ${kernel_addr_r} ${bootfile}; tftp ${fdt_addr_r} ${fdt_file}; run mmcargs addcons addmisc addmem; bootm ${kernel_addr_r} - ${fdt_addr_r}'
setenv net_nfs 'tftp ${kernel_addr_r} ${bootfile}; tftp ${fdt_addr_r} ${fdt_file}; run nfsargs addip addcons addmisc addmem; bootm ${kernel_addr_r} - ${fdt_addr_r}'
setenv netmask '255.255.255.0'
setenv nfsargs 'setenv bootargs root=/dev/nfs rw nfsroot=${serverip}:${rootpath}'
setenv rootpath '/opt/socfpga/nfs/rootfs-socrates/'
setenv serverip '192.168.42.20'
setenv stderr 'serial'
setenv stdin 'serial'
setenv stdout 'serial'
setenv verify 'n'
